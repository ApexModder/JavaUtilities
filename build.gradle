plugins {
	id 'se.bjurr.gitchangelog.git-changelog-gradle-plugin' version '1.71.8'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'

group 'xyz.apex.java.utility'
version '3.0.2-J17'
archivesBaseName = 'JavaUtilities'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
	mavenCentral()
}

dependencies {
	implementation 'com.google.code.findbugs:jsr305:3.0.2'
	implementation 'com.google.code.gson:gson:2.8.8'
	implementation 'com.google.guava:guava:31.0.1-jre'
	implementation 'commons-io:commons-io:2.11.0'
	implementation 'commons-logging:commons-logging:1.2'
	implementation 'org.apache.commons:commons-compress:1.21'
	implementation 'org.apache.commons:commons-lang3:3.12.0'
	implementation 'org.apache.httpcomponents:httpclient:4.5.13'
	implementation 'org.apache.httpcomponents:httpcore:4.4.14'
	implementation 'org.apache.logging.log4j:log4j-api:2.17.1'
	implementation 'org.apache.logging.log4j:log4j-core:2.17.1'
}

task sourcesJar(type: Jar) {
	classifier 'sources'
	from sourceSets.main.allJava
}

task deobfJar(type: Jar) {
	classifier 'deobf'
	from sourceSets.main.output
}

artifacts {
	archives jar
	archives sourcesJar
	archives deobfJar
}

jar {
	from sourceSets.main.output.classesDirs
	from sourceSets.main.output.resourcesDir
}

def previousGitTag = { ->
	def code = new ByteArrayOutputStream()

	exec {
		commandLine 'git', 'describe', '--tags', '--match', 'v*', '--exclude', "v${project.version}"
		standardOutput code
	}

	return code.toString().trim().split('-')[0]
}

task gitChangelogTask(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
	file = file("CHANGELOG.md")
	fromRepo = file('.')
	fromRef = "${previousGitTag}"
	// toRef = "v${project.version}"
	templateContent = file('.github/changelog.mustache').text
			.replace('{prj_version}', "${project.version}")
			.replace('{last_tag}', "${previousGitTag}")
}

tasks.withType(Jar) {
	from file('CHANGELOG.md')
	from file('LICENSE')

	manifest {
		attributes([
				'Specification-Title': "${archivesBaseName}",
				'Specification-Vendor': 'Apex',
				'Specification-Version': 'JAVA_17',

				'Implementation-Title': "${archivesBaseName}",
				'Implementation-Version': "${project.version}",
				'Implementation-Vendor' : 'Apex',
				'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

if(System.getenv('APEX_MODS_MAVEN_USERNAME') != null && System.getenv('APEX_MODS_MAVEN_PASSWORD') != null) {
	publishing {
		publications {
			mavenJava(MavenPublication) {
				groupId "${group}"
				artifactId "${archivesBaseName.toLowerCase(Locale.ROOT)}"
				version "${version.toLowerCase(Locale.ROOT)}"

				artifact jar
				artifact sourcesJar
				artifact deobfJar
			}
		}
		repositories {
			maven {
				name 'ApexMods-Maven'
				url 'https://apexmodder.jfrog.io/artifactory/mods-maven/'

				credentials {
					username System.getenv('APEX_MODS_MAVEN_USERNAME')
					password System.getenv('APEX_MODS_MAVEN_PASSWORD')
				}
			}
		}
	}
}