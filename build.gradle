apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'

group 'xyz.apex.java.utility'
version '1.0.2-J8'
archivesBaseName = 'JavaUtilities'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

repositories {
	mavenCentral()
}

dependencies {
	implementation 'com.google.code.findbugs:jsr305:3.0.2'
	implementation 'com.google.code.gson:gson:2.8.0'
	implementation 'com.google.guava:guava:21.0'
	implementation 'commons-io:commons-io:2.5'
	implementation 'commons-logging:commons-logging:1.1.3'
	implementation 'org.apache.commons:commons-compress:1.8.1'
	implementation 'org.apache.commons:commons-lang3:3.8.1'
	implementation 'org.apache.httpcomponents:httpclient:4.3.3'
	implementation 'org.apache.httpcomponents:httpcore:4.3.2'
	implementation 'org.apache.logging.log4j:log4j-api:2.11.2'
	implementation 'org.apache.logging.log4j:log4j-core:2.11.2'
}

task sourcesJar(type: Jar) {
	classifier 'sources'
	from sourceSets.main.allJava
}

task deobfJar(type: Jar) {
	classifier 'deobf'
	from sourceSets.main.output
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives jar
	archives sourcesJar
	archives deobfJar
	archives javadocJar
}

jar {
	from sourceSets.main.output.classesDirs
	from sourceSets.main.output.resourcesDir

	manifest {
		attributes([
				'Specification-Title': "${archivesBaseName}",
				'Specification-Vendor': 'Apex',
				'Specification-Version': 'JAVA_8',

				'Implementation-Title': "${archivesBaseName}",
				'Implementation-Version': "${project.version}",
				'Implementation-Vendor' : 'Apex',
				'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

publishing {
	if(System.getenv('GITHUB_TOKEN') != null) {
		publications {
			gpr(MavenPublication) {
				groupId "${group}"
				// GitHub Packages **require** names to be in lowercase
				artifactId "${archivesBaseName.toLowerCase(Locale.ROOT)}"
				version "${version.toLowerCase(Locale.ROOT)}"

				artifact jar
				artifact sourcesJar
				artifact deobfJar
				artifact javadocJar
			}
		}

		repositories {
			maven {
				name 'GitHubPackages'
				url uri('https://maven.pkg.github.com/ApexModder/JavaUtilities')

				credentials {
					username System.getenv('GITHUB_ACTOR')
					password System.getenv('GITHUB_TOKEN')
				}
			}
		}
	} else {
		publications {
			mavenJava(MavenPublication) {
				groupId "${group}"
				artifactId "${archivesBaseName}"
				version "${version}"

				artifact jar
				artifact sourcesJar
				artifact deobfJar
				artifact javadocJar
			}
		}
		repositories {
			mavenLocal()
		}
	}
}